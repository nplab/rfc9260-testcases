// Test Case Title : INIT - Bundling
// Test Case Id: T012_2 (Bundling - ESTABLISHED State)
// RFC Ref: 11.3 - SCTP Interactions with Firewalls (Version 2)
//
// The receiver of an INIT chunk MUST silently discard the INIT chunk and all further chunks 
// if the INIT chunk is bundles with other chunks 
// or the packets has a non zero verification Tag.
//

--tolerance_usecs=75000

#ifdef FreeBSD
 0.0 `kldload -n sctp`
+0.0 `sysctl -w net.inet.sctp.ecn_enable=0`
+0.0 `sysctl -w net.inet.sctp.pr_enable=0`
+0.0 `sysctl -w net.inet.sctp.asconf_enable=0`
+0.0 `sysctl -w net.inet.sctp.auth_enable=0`
+0.0 `sysctl -w net.inet.sctp.reconfig_enable=0`
+0.0 `sysctl -w net.inet.sctp.nrsack_enable=0`
+0.0 `sysctl -w net.inet.sctp.pktdrop_enable=0`
#endif
#ifdef Linux
 0.0 `modprobe sctp`
+0.0 `sysctl -w net.sctp.ecn_enable=0`
+0.0 `sysctl -w net.sctp.prsctp_enable=0`
#endif

+0.0 socket(..., SOCK_STREAM, IPPROTO_SCTP) = 3
+0.0 bind(3, ..., ...) = 0
+0.0 fcntl(3, F_GETFL) = 0x2 (flags O_RDWR)
+0.0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0 connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)
+0.0 > sctp: INIT[flgs=0, tag=1, a_rwnd=..., os=..., is=..., tsn=0,
                  SUPPORTED_ADDRESS_TYPES[types=...]]
+0.0 < sctp: INIT_ACK[flgs=0, tag=2, a_rwnd=1500, os=1, is=1, tsn=3,
                      STATE_COOKIE[len=4, val=...]]
+0.0 > sctp: COOKIE_ECHO[flgs=0, len=4, val=...]
+0.0 < sctp: COOKIE_ACK[flgs=0]
+0.0 getsockopt(3, SOL_SOCKET, SO_ERROR, [0], [4]) = 0
+0.5 < sctp: HEARTBEAT[flgs=0, HEARTBEAT_INFORMATION[len=8, val=...]]
+0.0 > sctp: HEARTBEAT_ACK[flgs=0, HEARTBEAT_INFORMATION[len=8, val=...]]

// Test in ESTABLISHED State
// Test INIT chunk bundled with other chunks (discard the INIT and all further chunks).
+0.1 < sctp: INIT[flgs=0, tag=1, a_rwnd=1500, os=1, is=1, tsn=1];
			 ERROR[flgs=0x00];
			 HEARTBEAT[flgs=0, HEARTBEAT_INFORMATION[len=8, val=...]];
			 DATA[flgs=BE, len=1016, tsn=1, sid=0, ssn=0, ppid=0]

// Test other combination of bundling
+0.1 < sctp: INIT[flgs=0, tag=1, a_rwnd=1500, os=1, is=1, tsn=1];
			 HEARTBEAT[flgs=0, HEARTBEAT_INFORMATION[len=8, val=...]];
			 SHUTDOWN[flgs=0, cum_tsn=2];
			 SHUTDOWN_ACK[flgs=0];
			 ERROR[flgs=0x00]
			  
			 			 
// To verify sending a new INIT & INIT ACK
+1.0 < sctp: INIT[flgs=0, tag=1, a_rwnd=1500, os=1, is=1, tsn=1]
+0.0 > sctp: INIT_ACK[flgs=..., tag=..., a_rwnd=..., os=..., is=..., tsn=..., ...]

// Tear down the association
+0.0 close(3) = 0
+0.0 > sctp: SHUTDOWN[flgs=0, cum_tsn=2]
+0.0 < sctp: SHUTDOWN_ACK[flgs=0]
+0.0 > sctp: SHUTDOWN_COMPLETE[flgs=0]




