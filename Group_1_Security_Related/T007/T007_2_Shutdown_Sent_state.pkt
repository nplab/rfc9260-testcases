// Test Case Title: Unexpected INIT in States Other than CLOSED, COOKIE-ECHOED, COOKIE WAIT & SHUTDOWN-ACK-SENT
// Test Case ID: T007_2 (SHUTDOWN_SENT State)
// RFC Ref : 5.2.2 - Unexpected INIT in States Other than CLOSED, COOKIE-ECHOED, COOKIE WAIT & SHUTDOWN-ACK-SENT (version 14)
//
// The verification Tag field in the common header of the outbound SCTP packet containing the ABORT chunk 
// MUST be set to the Initiate Tag value of the received INIT or INIT ACK chunk this ABORT chunk is responding to.
//
//

--tolerance_usecs=75000

#ifdef FreeBSD
 0.0 `kldload -n sctp`
+0.0 `sysctl -w net.inet.sctp.ecn_enable=0`
+0.0 `sysctl -w net.inet.sctp.pr_enable=0`
+0.0 `sysctl -w net.inet.sctp.asconf_enable=0`
+0.0 `sysctl -w net.inet.sctp.auth_enable=0`
+0.0 `sysctl -w net.inet.sctp.reconfig_enable=0`
+0.0 `sysctl -w net.inet.sctp.nrsack_enable=0`
+0.0 `sysctl -w net.inet.sctp.pktdrop_enable=0`
#endif
#ifdef Linux
 0.0 `modprobe sctp`
+0.0 `sysctl -w net.sctp.ecn_enable=0`
+0.0 `sysctl -w net.sctp.prsctp_enable=0`
#endif

+0.0 socket(..., SOCK_STREAM, IPPROTO_SCTP) = 3
+0.0 bind(3, ..., ...) = 0
+0.0 fcntl(3, F_GETFL) = 0x2 (flags O_RDWR)
+0.0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0 connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)
+0.0 > sctp: INIT[flgs=0, tag=1, a_rwnd=..., os=..., is=..., tsn=0,
                  SUPPORTED_ADDRESS_TYPES[types=...]]
+0.0 < sctp: INIT_ACK[flgs=0, tag=2, a_rwnd=1500, os=1, is=1, tsn=3,
                      STATE_COOKIE[len=4, val=...]]
+0.0 > sctp: COOKIE_ECHO[flgs=0, len=4, val=...]
+0.0 < sctp: COOKIE_ACK[flgs=0]
+0.0 getsockopt(3, SOL_SOCKET, SO_ERROR, [0], [4]) = 0
//
// Tear down the association
+0.0 shutdown(3, SHUT_WR) = 0
+0.0 > sctp: SHUTDOWN[flgs=0, cum_tsn=2]

// Unexpected INIT in SHUTDOWN-SENT state:

+0.0 < sctp: INIT[flgs=0, tag=3, a_rwnd=1500, os=0, is=1, tsn=0]
+0.0 > sctp(tag=3): ABORT[flgs=0, INVALID_MANDATORY_PARAMETER[]]

+0.0 getsockopt(3, SOL_SOCKET, SO_ERROR, [0], [4]) = 0

//
//
+0.0 < sctp: SHUTDOWN_ACK[flgs=0]
+0.0 > sctp: SHUTDOWN_COMPLETE[flgs=0]
+0.0 close(3) = 0
